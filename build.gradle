// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-beta03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31"

        // There is a problem with google-services 4.2.0 so that the build will not success.
        // Upgrade this dependency when a fix is available.
        classpath 'com.google.gms:google-services:4.1.0'
    }
}
apply plugin: 'kotlin'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

configurations {
    ktlint
}

repositories {
    jcenter()
    mavenCentral()
}
dependencies {
    ktlint "com.pinterest:ktlint:0.33.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "app/src/**/*.kt", "binaryFileGenerator/src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "app/src/**/*.kt", "binaryFileGenerator/src/**/*.kt"
}